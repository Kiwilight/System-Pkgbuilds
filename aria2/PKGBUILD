# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=aria2
pkgver=1.14.2
pkgrel=1
pkgdesc='Download utility that supports HTTP(S), FTP, BitTorrent, and Metalink'
arch=('i686' 'x86_64')
url='http://aria2.sourceforge.net/'
license=('GPL')
depends=('gnutls' 'libxml2' 'sqlite3' 'c-ares' 'ca-certificates')
checkdepends=('cppunit')
source=("http://downloads.sourceforge.net/aria2/aria2-${pkgver}.tar.bz2")
md5sums=('19b1a9f83a09c6ef5c7ab87e4e0f7974')

build() {
  cd aria2-${pkgver}

  ./configure \
    --prefix=/usr \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt

  make
}

check() {
  cd aria2-${pkgver}

  make check
}

package() {
  cd aria2-${pkgver}

  make DESTDIR=${pkgdir} install
}
